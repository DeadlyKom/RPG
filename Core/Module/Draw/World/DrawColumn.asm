                ifndef _CORE_MODULE_DRAW_WORLD_DRAW_COLUMN_
                define _CORE_MODULE_DRAW_WORLD_DRAW_COLUMN_

                module World
; -----------------------------------------
; отрисовка тайлов карты мира по вертикали 
; In:
;   DE  - адрес буфера отображения (RenderBuffer)
;   HL' - адрес экрана вывода
;   IX  - адрес функции Up
;   IY  - адрес функции Down
; Out:
; Corrupt:
; Note:
;   установить RestoreBC
;   установить 7 страницу
;   устойчивый к прерываниям
; -----------------------------------------
DrawColumn:     ; сохранение стека
                LD (.ContainerSP_0), SP
                LD (.ContainerSP_1), SP
                LD (.ContainerSP_2), SP

                LD A, (.Shift_Y)
                OR A
                JR Z, .Aligned

                ; чтение индекса тайла карты
                LD A, (DE)
                INC E

                ; -----------------------------------------
                ;   значение в аккумуляторе
                ;      7    6    5    4    3    2    1    0
                ;   +----+----+----+----+----+----+----+----+
                ;   | L3 | L2 | L1 | L0 | R3 | R2 | R1 | R0 |
                ;   +----+----+----+----+----+----+----+----+
                ;
                ;   L3-L0   [7..4]  - номер левого тайла
                ;   R3-R0   [3..0]  - номер правого тайла
                ; -----------------------------------------
                
                EXX
                EX DE, HL

                ; поиск тайлопары в таблице
                LD H, HIGH Adr.WorldTilepair
                LD L, A
                LD L, (HL)

                ; -----------------------------------------
                ;   значение в регистре L
                ;
                ;      7    6    5    4    3    2    1    0
                ;   +----+----+----+----+----+----+----+----+
                ;   | 0  | T6 | T5 | T4 | T3 | T2 | T1 | T0 |
                ;   +----+----+----+----+----+----+----+----+
                ;
                ;   T6-T0   [6..0]  - номер тайла от 0 до 127
                ; -----------------------------------------

.Shift          EQU $+1
                LD H, #00
                ; -----------------------------------------
                ;   значение в регистре H
                ;      7    6    5    4    3    2    1    0
                ;   +----+----+----+----+----+----+----+----+
                ;   | 1  | 1  | A3 | A2 | A1 | A0 | S1 | S0 |
                ;   +----+----+----+----+----+----+----+----+
                ;
                ;   A3-A0   [7..2]  - адрес таблицы (13..10 биты адреса)
                ;   S1,S0   [1,0]   - номер сдвига
                ; -----------------------------------------

                ; -----------------------------------------
                ;   регистровая пара HL хранит адрес 2х значений
                ;   номер тайла + смещение
                ;       +----+----+----+----+----+----+----+----+   +----+----+----+----+----+----+----+----+
                ;       | 15 | 14 | 13 | 12 | 11 | 10 |  9 |  8 |   |  7 |  6 |  5 |  4 |  3 |  2 |  1 |  0 |
                ;       +----+----+----+----+----+----+----+----+   +----+----+----+----+----+----+----+----+
                ;       | 1  | 1  | A3 | A2 | A1 | A0 | S1 | S0 |   | 0  | T6 | T5 | T4 | T3 | T2 | T1 | T0 |
                ;       +----+----+----+----+----+----+----+----+   +----+----+----+----+----+----+----+----+
                ;
                ;   A3-A0   [10..13]- адрес таблицы
                ;   S1,S0   [1,0]   - номер сдвига
                ; -----------------------------------------
                
                ; -----------------------------------------
                ; чтение адреса спрайта
                ; -----------------------------------------
.OffsetSpr      EQU $+1
                LD A, #00                                                       ; смещение в спрайте
                ADD A, (HL)
                SET 7, L
                LD H, (HL)
                LD L, A
                ; -----------------------------------------
                ;   форма адреса спрайта, взятая из таблицы
                ;       +----+----+----+----+----+----+----+----+   +----+----+----+----+----+----+----+----+
                ;       | 15 | 14 | 13 | 12 | 11 | 10 |  9 |  8 |   |  7 |  6 |  5 |  4 |  3 |  2 |  1 |  0 |
                ;       +----+----+----+----+----+----+----+----+   +----+----+----+----+----+----+----+----+
                ;       | A10| A9 | A8 | A7 | A6 | A5 | A4 | A3 |   | A2 | A1 | A0 | O3 | O2 | O1 | O0 | 0  |
                ;       +----+----+----+----+----+----+----+----+   +----+----+----+----+----+----+----+----+
                ;   A10-A0  [15..5] - адрес расположения спрайта
                ;   O3-O0   [4..1]  - смещение в спрайте
                ; -----------------------------------------
                
                ; -----------------------------------------
                ; защита от повреждения данных во время прерывания
                ; -----------------------------------------

                ; копирование данных 2-х байт спрайта
                LD C, (HL)
                INC HL
                LD B, (HL)
                INC HL

                ; инициализация стека, указывает на спрайт тайла
                LD SP, HL
                EX DE, HL

                ;   HL' - не используется
                ;   DE' - адрес буфера отображения (RenderBuffer)
                ;   B'  - счётчик тайлов отрисовки по вертикали
                ;   C'  - cчётчик тайлов отрисовки по горизонтали
                ;   HL  - адрес экрана вывода
                ;   DE  - не используется
                ;   BC  - первый два байта спрайта
                ;   SP  - адрес спрайта +2
                ;   IX  - адрес функции Up
                ;   IY  - адрес функции Down

.Shift_Y        EQU $+1
                LD A, #00
                CP #08
                JR C, .DrawDown_

                LD DE, .DrawDown
.x8             EQU $+1
                JP #0000

.DrawDown       LD A, (.Shift_Y)
                AND #07
                JR Z, .DrawCompleted

                ; -----------------------------------------
                ; рисование нижней части тайла
                ; -----------------------------------------

                EX DE, HL
                POP BC

                ; классический метод "DOWN_HL" 25/59
                INC H
                LD A, H
                AND #07
                JP NZ, $+12
                LD A, L
                SUB #E0
                LD L, A
                SBC A, A
                AND #F8
                ADD A, H
                LD H, A

.DrawDown_      LD DE, .DrawCompleted
                JP (IY)

.DrawCompleted  ; -----------------------------------------
                ; рисование тайла завершено
                ; -----------------------------------------
                EX DE, HL

                ; классический метод "DOWN_HL" 25/59
                INC H
                LD A, H
                AND #07
                JP NZ, $+12
                LD A, L
                SUB #E0
                LD L, A
                SBC A, A
                AND #F8
                ADD A, H
                LD H, A

.ContainerSP_0  EQU $+1
                LD SP, #0000
                EXX

                DEC E
.Aligned        INC E
                ; -----------------------------------------
                ; выровнен по 
                ; -----------------------------------------

                ; инициализация
                LD B, SCR_WORLD_SIZE_Y-1

.Loop           ; чтение индекса тайла карты
                LD A, (DE)
                INC E

                ; -----------------------------------------
                ;   значение в аккумуляторе
                ;      7    6    5    4    3    2    1    0
                ;   +----+----+----+----+----+----+----+----+
                ;   | L3 | L2 | L1 | L0 | R3 | R2 | R1 | R0 |
                ;   +----+----+----+----+----+----+----+----+
                ;
                ;   L3-L0   [7..4]  - номер левого тайла
                ;   R3-R0   [3..0]  - номер правого тайла
                ; -----------------------------------------
                
                EXX
                EX DE, HL

                ; поиск тайлопары в таблице
                LD H, HIGH Adr.WorldTilepair
                LD L, A
                LD L, (HL)

                ; -----------------------------------------
                ;   значение в регистре L
                ;
                ;      7    6    5    4    3    2    1    0
                ;   +----+----+----+----+----+----+----+----+
                ;   | 0  | T6 | T5 | T4 | T3 | T2 | T1 | T0 |
                ;   +----+----+----+----+----+----+----+----+
                ;
                ;   T6-T0   [6..0]  - номер тайла от 0 до 127
                ; -----------------------------------------

.ShiftLoop      EQU $+1
                LD H, #00
                ; -----------------------------------------
                ;   значение в регистре H
                ;      7    6    5    4    3    2    1    0
                ;   +----+----+----+----+----+----+----+----+
                ;   | 1  | 1  | A3 | A2 | A1 | A0 | S1 | S0 |
                ;   +----+----+----+----+----+----+----+----+
                ;
                ;   A3-A0   [7..2]  - адрес таблицы (13..10 биты адреса)
                ;   S1,S0   [1,0]   - номер сдвига
                ; -----------------------------------------

                ; -----------------------------------------
                ;   регистровая пара HL хранит адрес 2х значений
                ;   номер тайла + смещение
                ;       +----+----+----+----+----+----+----+----+   +----+----+----+----+----+----+----+----+
                ;       | 15 | 14 | 13 | 12 | 11 | 10 |  9 |  8 |   |  7 |  6 |  5 |  4 |  3 |  2 |  1 |  0 |
                ;       +----+----+----+----+----+----+----+----+   +----+----+----+----+----+----+----+----+
                ;       | 1  | 1  | A3 | A2 | A1 | A0 | S1 | S0 |   | 0  | T6 | T5 | T4 | T3 | T2 | T1 | T0 |
                ;       +----+----+----+----+----+----+----+----+   +----+----+----+----+----+----+----+----+
                ;
                ;   A3-A0   [10..13]- адрес таблицы
                ;   S1,S0   [1,0]   - номер сдвига
                ; -----------------------------------------
                
                ; -----------------------------------------
                ; чтение адреса спрайта
                ; -----------------------------------------

                LD A, (HL)
                SET 7, L
                LD H, (HL)
                LD L, A
                ; -----------------------------------------
                ;   форма адреса спрайта, взятая из таблицы
                ;       +----+----+----+----+----+----+----+----+   +----+----+----+----+----+----+----+----+
                ;       | 15 | 14 | 13 | 12 | 11 | 10 |  9 |  8 |   |  7 |  6 |  5 |  4 |  3 |  2 |  1 |  0 |
                ;       +----+----+----+----+----+----+----+----+   +----+----+----+----+----+----+----+----+
                ;       | A10| A9 | A8 | A7 | A6 | A5 | A4 | A3 |   | A2 | A1 | A0 | 0  | 0  | 0  | 0  | 0  |
                ;       +----+----+----+----+----+----+----+----+   +----+----+----+----+----+----+----+----+
                ;   A10-A0  [15..5] - адрес расположения спрайта
                ;   O3-O0   [4..1]  - смещение в спрайте
                ; -----------------------------------------

                ; -----------------------------------------
                ; защита от повреждения данных во время прерывания
                ; -----------------------------------------

                ; копирование данных 2-х байт спрайта
                LD C, (HL)
                INC HL
                LD B, (HL)
                INC HL

                ; инициализация стека, указывает на спрайт тайла
                LD SP, HL
                EX DE, HL

                ;   HL' - не используется
                ;   DE' - адрес буфера отображения (RenderBuffer)
                ;   B'  - счётчик тайлов отрисовки по вертикали
                ;   C'  - не используется
                ;   HL  - адрес экрана вывода
                ;   DE  - не используется
                ;   BC  - первый два байта спрайта
                ;   SP  - адрес спрайта +2
                ;   IX  - адрес функции Up
                ;   IY  - адрес функции Down
                
                ; -----------------------------------------
                ; рисование верхней части тайла
                ; -----------------------------------------
                LD DE, .DrawCenterLoop
                JP (IX)

.DrawCenterLoop ; -----------------------------------------
                ; рисование средней части тайла
                ; -----------------------------------------

                EX DE, HL
                POP BC

                ; -----------------------------------------
                ; переход на след знакоместо
                ; -----------------------------------------

                ; классический метод "DOWN_HL" 25/59
                INC H
                LD A, H
                AND #07
                JP NZ, $+12
                LD A, L
                SUB #E0
                LD L, A
                SBC A, A
                AND #F8
                ADD A, H
                LD H, A

                LD DE, .DrawDownLoop
.x8_Loop        EQU $+1
                JP #0000

.DrawDownLoop   LD A, (.Shift_Y)
                AND #07
                JR Z, .DrawCompletedL
                
                ; -----------------------------------------
                ; рисование нижней части тайла
                ; -----------------------------------------

                EX DE, HL
                POP BC

                ; -----------------------------------------
                ; переход на след знакоместо
                ; -----------------------------------------

                ; классический метод "DOWN_HL" 25/59
                INC H
                LD A, H
                AND #07
                JP NZ, $+12
                LD A, L
                SUB #E0
                LD L, A
                SBC A, A
                AND #F8
                ADD A, H
                LD H, A

                LD DE, .DrawCompletedL
                JP (IY)

.DrawCompletedL ; -----------------------------------------
                ; рисование тайла завершено
                ; -----------------------------------------
                
                EX DE, HL

                ; -----------------------------------------
                ; переход на след знакоместо
                ; -----------------------------------------

                ; классический метод "DOWN_HL" 25/59
                INC H
                LD A, H
                AND #07
                JP NZ, $+12
                LD A, L
                SUB #E0
                LD L, A
                SBC A, A
                AND #F8
                ADD A, H
                LD H, A

.ContainerSP_1  EQU $+1
                LD SP, #0000

                EXX
                DJNZ .Loop

                ; чтение индекса тайла карты
                LD A, (DE)
                INC E

                ; -----------------------------------------
                ;   значение в аккумуляторе
                ;      7    6    5    4    3    2    1    0
                ;   +----+----+----+----+----+----+----+----+
                ;   | L3 | L2 | L1 | L0 | R3 | R2 | R1 | R0 |
                ;   +----+----+----+----+----+----+----+----+
                ;
                ;   L3-L0   [7..4]  - номер левого тайла
                ;   R3-R0   [3..0]  - номер правого тайла
                ; -----------------------------------------
                
                EXX
                EX DE, HL

                ; поиск тайлопары в таблице
                LD H, HIGH Adr.WorldTilepair
                LD L, A
                LD L, (HL)

                ; -----------------------------------------
                ;   значение в регистре L
                ;
                ;      7    6    5    4    3    2    1    0
                ;   +----+----+----+----+----+----+----+----+
                ;   | 0  | T6 | T5 | T4 | T3 | T2 | T1 | T0 |
                ;   +----+----+----+----+----+----+----+----+
                ;
                ;   T6-T0   [6..0]  - номер тайла от 0 до 127
                ; -----------------------------------------

.ShiftEnd       EQU $+1
                LD H, #00
                ; -----------------------------------------
                ;   значение в регистре H
                ;      7    6    5    4    3    2    1    0
                ;   +----+----+----+----+----+----+----+----+
                ;   | 1  | 1  | A3 | A2 | A1 | A0 | S1 | S0 |
                ;   +----+----+----+----+----+----+----+----+
                ;
                ;   A3-A0   [7..2]  - адрес таблицы (13..10 биты адреса)
                ;   S1,S0   [1,0]   - номер сдвига
                ; -----------------------------------------

                ; -----------------------------------------
                ;   регистровая пара HL хранит адрес 2х значений
                ;   номер тайла + смещение
                ;       +----+----+----+----+----+----+----+----+   +----+----+----+----+----+----+----+----+
                ;       | 15 | 14 | 13 | 12 | 11 | 10 |  9 |  8 |   |  7 |  6 |  5 |  4 |  3 |  2 |  1 |  0 |
                ;       +----+----+----+----+----+----+----+----+   +----+----+----+----+----+----+----+----+
                ;       | 1  | 1  | A3 | A2 | A1 | A0 | S1 | S0 |   | 0  | T6 | T5 | T4 | T3 | T2 | T1 | T0 |
                ;       +----+----+----+----+----+----+----+----+   +----+----+----+----+----+----+----+----+
                ;
                ;   A3-A0   [10..13]- адрес таблицы
                ;   S1,S0   [1,0]   - номер сдвига
                ; -----------------------------------------
                
                ; -----------------------------------------
                ; чтение адреса спрайта
                ; -----------------------------------------

                LD A, (HL)
                SET 7, L
                LD H, (HL)
                LD L, A
                ; -----------------------------------------
                ;   форма адреса спрайта, взятая из таблицы
                ;       +----+----+----+----+----+----+----+----+   +----+----+----+----+----+----+----+----+
                ;       | 15 | 14 | 13 | 12 | 11 | 10 |  9 |  8 |   |  7 |  6 |  5 |  4 |  3 |  2 |  1 |  0 |
                ;       +----+----+----+----+----+----+----+----+   +----+----+----+----+----+----+----+----+
                ;       | A10| A9 | A8 | A7 | A6 | A5 | A4 | A3 |   | A2 | A1 | A0 | 0  | 0  | 0  | 0  | 0  |
                ;       +----+----+----+----+----+----+----+----+   +----+----+----+----+----+----+----+----+
                ;   A10-A0  [15..5] - адрес расположения спрайта
                ;   O3-O0   [4..1]  - смещение в спрайте
                ; -----------------------------------------

                ; -----------------------------------------
                ; защита от повреждения данных во время прерывания
                ; -----------------------------------------

                ; копирование данных 2-х байт спрайта
                LD C, (HL)
                INC HL
                LD B, (HL)
                INC HL

                ; инициализация стека, указывает на спрайт тайла
                LD SP, HL
                EX DE, HL

                ;   HL' - не используется
                ;   DE' - адрес буфера отображения (RenderBuffer)
                ;   B'  - счётчик тайлов отрисовки по вертикали
                ;   C'  - не используется
                ;   HL  - адрес экрана вывода
                ;   DE  - не используется
                ;   BC  - первый два байта спрайта
                ;   SP  - адрес спрайта +2
                ;   IX  - адрес функции Up
                ;   IY  - адрес функции Down
                
                ; -----------------------------------------
                ; рисование верхней части тайла
                ; -----------------------------------------
                LD DE, .DrawCenterEnd
                JP (IX)

.DrawCenterEnd  LD A, (.Shift_Y)
                CP #08
                JR NC, .DrawDownEnd

                ; -----------------------------------------
                ; рисование средней части тайла
                ; -----------------------------------------

                EX DE, HL
                POP BC

                ; классический метод "DOWN_HL" 25/59
                INC H
                LD A, H
                AND #07
                JP NZ, $+12
                LD A, L
                SUB #E0
                LD L, A
                SBC A, A
                AND #F8
                ADD A, H
                LD H, A

                LD DE, .DrawDownEnd
.x8_End         EQU $+1
                JP #0000

.DrawDownEnd    
.ContainerSP_2  EQU $+1
                LD SP, #0000
                EXX

                RET

                display " - Draw column:\t\t\t\t\t", /A, DrawColumn, " = busy [ ", /D, $ - DrawColumn, " bytes  ]"

                endmodule

                endif ; ~ _CORE_MODULE_DRAW_WORLD_DRAW_COLUMN_
