                ifndef _CORE_MODULE_DRAW_WORLD_DRAW_COLUMN_
                define _CORE_MODULE_DRAW_WORLD_DRAW_COLUMN_

                module World
; -----------------------------------------
; отрисовка тайлов карты мира по вертикали 
; In:
;   DE  - адрес буфера отображения (RenderBuffer)
;   HL' - адрес экрана вывода
;   IX  - адрес функции Up
;   IY  - адрес функции Down
; Out:
; Corrupt:
; Note:
;   устойчивый к прерываниям
; -----------------------------------------
DrawColumn:     ; инициализация
                LD (.ContainerSP), SP
                LD B, SCR_WORLD_SIZE_Y-1

.Loop           ; чтение индекса тайла карты
                LD A, (DE)
                AND %00001111                                                   ; ToDo удалить, индексы все в пределах от 0 до 31
                INC E

                ; -----------------------------------------
                ; расчёт адреса спрайта тайла
                ; -----------------------------------------
                ;       +----+----+----+----+----+----+----+----+   +----+----+----+----+----+----+----+----+
                ;       | 15 | 14 | 13 | 12 | 11 | 10 |  9 |  8 |   |  7 |  6 |  5 |  4 |  3 |  2 |  1 |  0 |
                ;       +----+----+----+----+----+----+----+----+   +----+----+----+----+----+----+----+----+
                ;       | .. | .. | .. | .. | T4 | T3 | T2 | T1 |   | T0 | S1 | S0 |  0 |  0 |  0 |  0 |  0 |
                ;       +----+----+----+----+----+----+----+----+   +----+----+----+----+----+----+----+----+
                ;   T4-T0   [11..7] - номер тайла от 0 до 31
                ;   S1,S0   [6,5]   - номер сдвига
                ; -----------------------------------------
                EXX
                EX DE, HL
                RRA
.Shift          EQU $+1
                LD L, 00        ; Shift_X
                RR L
                OR HIGH Adr.WorldSpr                                            ; обязательно выровнен 4096 байт
                LD H, A

                ; -----------------------------------------
                ; защита от повреждения данных во время прерывания
                ; -----------------------------------------

                ; копирование данных 2-х байт спрайта
                LD C, (HL)
                INC HL
                LD B, (HL)
                INC HL

                ; инициализация стека, указывает на спрайт тайла
                LD SP, HL
                EX DE, HL

                ;   HL' - не используется
                ;   DE' - адрес буфера отображения (RenderBuffer)
                ;   B'  - счётчик тайлов отрисовки по вертикали
                ;   C'  - не используется
                ;   HL  - адрес экрана вывода
                ;   DE  - не используется
                ;   BC  - первый два байта спрайта
                ;   SP  - адрес спрайта +2
                ;   IX  - адрес функции Up
                ;   IY  - адрес функции Down
                
                ; -----------------------------------------
                ; рисование верхней части тайла
                ; -----------------------------------------
                LD DE, .DrawCenter
                JP (IX)

.DrawCenter     ; -----------------------------------------
                ; рисование средней части тайла
                ; -----------------------------------------

                EX DE, HL
                POP BC

                ; -----------------------------------------
                ; переход на след знакоместо
                ; -----------------------------------------

                ; классический метод "DOWN_HL" 25/59
                INC H
                LD A, H
                AND #07
                JP NZ, $+12
                LD A, L
                SUB #E0
                LD L, A
                SBC A, A
                AND #F8
                ADD A, H
                LD H, A

                LD DE, .DrawDown
.x8             EQU $+1
                JP #0000

.DrawDown       ; -----------------------------------------
                ; рисование нижней части тайла
                ; -----------------------------------------

                EX DE, HL
                POP BC

                ; -----------------------------------------
                ; переход на след знакоместо
                ; -----------------------------------------
                LD A, L
                ADD A, #20
                LD L, A
                LD A, H
                AND %11111000
                LD H, A

                LD DE, .DrawCompleted
                JP (IY)

.DrawCompleted  ; -----------------------------------------
                ; рисование тайла завершено
                ; -----------------------------------------
                
                EX DE, HL
                ; INC H
                
.ContainerSP    EQU $+1
                LD SP, #0000

                ; -----------------------------------------
                ; переход на след знакоместо
                ; -----------------------------------------

                EXX
                DJNZ .Loop

                RET

                display " - Draw Column : \t\t\t\t\t", /A, DrawColumn, " = busy [ ", /D, $ - DrawColumn, " bytes  ]"

                endmodule

                endif ; ~ _CORE_MODULE_DRAW_WORLD_DRAW_COLUMN_
