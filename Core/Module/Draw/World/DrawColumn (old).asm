; -----------------------------------------
; отрисовка столбца тайлов карты мира
; In:
;   HL' - адрес экрана вывода
;   B'  - количество строк в знакоместе
;   IX  - адрес буфера отображения(RenderBuffer)
;   IY  - адрес функции рисования тайла
; Out:
; Corrupt:
; Note:
;   устойчивый к прерываниям
; -----------------------------------------
DrawColumn:    ; инициализация
                LD B, SCR_WORLD_SIZE_Y

.Loop           ; чтение индекса тайла карты
                LD A, (IX + 0)
                ADD A, A
                AND %00000111
                INC IX

                ; -----------------------------------------
                ; расчёт адреса спрайта тайла
                ; -----------------------------------------

                ; расчёт адреса спрайта тайла
                LD H, HIGH Adr.WorldSpr
                LD L, A
                LD A, (HL)
                INC L
                LD H, (HL)
.Offset         EQU $+1
                ADD A, #00
                LD L, A
                ADC A, H
                SUB L
                LD H, A

                ; инициализация стека, указывает на спрайт тайла
                LD SP, HL

                EXX
                LD C, (WORLD_TILE_PIX_SIZE >> 1)
                JP (IY)

.Finish         ; выход из функции отрисовки
.ContainerSP    EQU $+1
                LD SP, #0000
.Rows_x2        EQU $+1
                LD B, #04                                                       ; количество двойных строк в знакоместе
                EXX
                DJNZ .Loop
                RET
