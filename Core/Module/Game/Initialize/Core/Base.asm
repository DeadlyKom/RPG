
                ifndef _MODULE_GAME_INITIALIZE_CORE_
                define _MODULE_GAME_INITIALIZE_CORE_
; -----------------------------------------
; базовая инициализация
; In:
; Out:
; Corrupt:
; Note:
; -----------------------------------------
Base:           ; -----------------------------------------
                ; установка бордюра
                ; -----------------------------------------
                BORDER BLACK

                ; -----------------------------------------
                ; подготовка теневого экрана
                ; -----------------------------------------
                LD HL, .CLS
                LD DE, Adr.SharedBuffer
                LD BC, .SizeCLS
                CALL Memcpy.FastLDIR
                CALL Adr.SharedBuffer

                ; -----------------------------------------
                ; подготовка основного экрана
                ; -----------------------------------------
                SHOW_SHADOW_SCREEN                                              ; отобразить теневой экран
                CLS_4000
                ATTR_4000_IPB WHITE, BLACK, 0

                ; -----------------------------------------
                ; установка дефолтной конфигурации
                ; -----------------------------------------
                LD HL, .DefaultConfig
                LD DE, Adr.GameConfig
                LD BC, Size.GameConfig
                CALL Memcpy.FastLDIR

                ; -----------------------------------------
                ; очистка состояний игры
                ; -----------------------------------------
                LD HL, Adr.GameState + Size.GameState
                LD DE, #0000
                CALL SafeFill.b32

                ; -----------------------------------------
                ; инициализация таблиц
                ; -----------------------------------------
                CALL Packs.Tables.Gen_ScrAdr                                    ; генерация адресов экрана
                CALL Packs.Tables.Gen_ShiftTable                                ; генерация таблицы сдвигов
                JP Packs.Tables.Gen_MulSprTable                                 ; генерация таблицы умножения для спрайтов

.CLS            ; -----------------------------------------
                ; блок кода очистки теневого экрана
                ; -----------------------------------------
                SET_SCREEN_SHADOW                                               ; включение страницы теневого экрана
                CLS_C000
                ATTR_C000_IPB WHITE, BLACK, 0
                SET_PAGE_BLOK_6                                                 ; включение страницы с блоком кода
                RET
.SizeCLS        EQU $-.CLS

.DefaultConfig  FGameConfig {
                VK_ENTER,                                                       ; клавиша по умолчанию "меню/пауза"
                VK_CAPS_SHIFT,                                                  ; клавиша по умолчанию "ускорить"
                VK_SYMBOL_SHIFT,                                                ; клавиша по умолчанию "oтмена"
                VK_SPACE,                                                       ; клавиша по умолчанию "выбор"
                VK_D,                                                           ; клавиша по умолчанию "вправо"
                VK_A,                                                           ; клавиша по умолчанию "влево"
                VK_S,                                                           ; клавиша по умолчанию "вниз"
                VK_W,                                                           ; клавиша по умолчанию "вверх"
                INPUT_KEYBOARD,                                                 ; флаги
                G_PARTICLE_OFF,
                #C0                                                             ; старший адрес экрана
                {#00, #00},
                SCR_WORLD_POS_Y,                                                ; верхняя граница вывода спрайта    (в знакоместах)
                SCR_WORLD_POS_Y + (SCR_WORLD_SIZE_Y << 1),                      ; нижняя граница вывода спрайта     (в знакоместах)
                SCR_WORLD_POS_X,                                                ; левая граница вывода спрайтов     (в знакоместах)
                SCR_WORLD_POS_X + (SCR_WORLD_SIZE_X << 1) - 1,                  ; правая граница вывода спрайтов    (в знакоместах)
                #00,
                #0000, #0000,
                #00
                }

                endif ; ~_MODULE_GAME_INITIALIZE_CORE_
